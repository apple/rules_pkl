//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//   https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//

amends "@pkl.impl.ghactions/PklCI.pkl"

import "@pkl.impl.ghactions/jobs/HawkeyeCheck.pkl"
import "@gha/actions/Common.pkl"
import "@gha/Context.pkl"
import "@gha/Workflow.pkl"

testReports {
  junit {
    "bazel-testlogs/**/*.xml"
  }
  excludeJobs {
    "check-hawkeye"
  }
}

local test: Workflow = new {
  jobs {
    ["test"] {
      `runs-on` = "ubuntu-latest"
      steps {
        new Common.Checkout {}
        new {
          uses = "bazel-contrib/setup-bazel@4fd964a13a440a8aeb0be47350db2fc640f19ca8"
          with {
            ["bazelisk-cache"] = true
            ["disk-cache"] = "${{ matrix.os }}-cache"
            ["repository-cache"] = true
          }
        }
        new {
          name = "Buildifier lint"
          run = "bazel run --config=ci buildifier"
        }
        new {
          name = "Check gazelle"
          run =
            """
            bazel run --config=ci //:gazelle
            git diff --exit-code
            """
        }
        new {
          name = "Bazel version consistency"
          env {
            ["WORKING_DIRECTORY"] = Context.github.workspace
          }
          run = "bazel run --config=ci //tests/version:version_test -- --verbose"
        }
        new {
          name = "Update integration test repos .bazelrc (debug information and caching)"
          run = """
            # Assign a separate output base to the integration tests: it needs to be explicitly set, and set as a
            # different from the root workspace output base (which is set in the home bazelrc) to avoid a deadlock.
            # It also needs to be the same directory for all the integration tests, otherwise we run out of space on
            # the default GHA runners.
            # We use the `bazel_integration` configuration so it takes precedence over the generic home rc declaration.
            find tests/integration_tests/example_workspaces -name '.bazelrc' | while read -r f; do
              printf '\\nstartup --output_base=/home/runner/.bazel-integration\\n' >> "$f"
              printf 'common --announce_rc --curses=no --color=yes --show_timestamps --test_output=streamed\\n' >> "$f"
            done

            # Extract the `startup --output_base=...` line from the home .bazelrc to an explicit .bazelrc, otherwise its
            # precedence is too high and integration tests pick up the value.
            #
            output_base_line=$(grep '^startup --output_base' /home/runner/.bazelrc | tail -1)
            sed -i '/^startup --output_base/d' /home/runner/.bazelrc
            printf '%s\\n' "$output_base_line" > /home/runner/.bazelrc.cmdline
            """
        }
        new {
          name = "Bazel test"
          run = "bazel --bazelrc=/home/runner/.bazelrc.cmdline test --config=ci -- //... -//tests/integration_tests/..."
        }
      }
    }
    ["check-hawkeye"] = new HawkeyeCheck {}.job
  }
}

prb = test

build = test

main = test
